AC_PREREQ(2.59)
AC_INIT([midgardcr-core],[10.05.99])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_SRCDIR(src/midgard.c)

AC_SUBST(MIDGARD_ABI_MAJOR, 3)
AC_SUBST(MIDGARD_ABI_MINOR, 0)
AC_SUBST(MIDGARD_RELEASE_MAJOR, 2010)
AC_SUBST(MIDGARD_RELEASE_MINOR, 11)

AM_INIT_AUTOMAKE([-Wno-portability])
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

AM_MAINTAINER_MODE
GOBJECT_INTROSPECTION_CHECK([0.6.5])

AC_PROG_CC
AM_PROG_LIBTOOL
AM_PROG_VALAC([0.7.0])
AC_PROG_INSTALL

_MIDGARD_DBUS_SUPPORT=yes
dbus_libs="dbus-1 dbus-glib-1"
MIDGARD_DBUS_SUPPORT=1
AC_MSG_CHECKING([whether to compile dbus support (enabled by default)])
AC_ARG_WITH(dbus-support,
	[  --with-dbus-support    compile with dbus support ],
	_MIDGARD_DBUS_SUPPORT=[$]withval, _MIDGARD_DBUS_SUPPORT="yes")
AC_MSG_RESULT($_MIDGARD_DBUS_SUPPORT)
if test "$_MIDGARD_DBUS_SUPPORT" == "no"; then
	#MIDGARD_DBUS_SUPPORT=0
	dbus_libs=""
fi

PKG_CHECK_MODULES(MIDGARD, glib-2.0 gobject-2.0 libgda-4.0 $dbus_libs)

DBUS_CONF_DIR="/etc"
if test "$_MIDGARD_DBUS_SUPPORT" == "yes"; then
	#MIDGARD_DBUS_SUPPORT=1
	MIDGARD_CFLAGS=" ${MIDGARD_CFLAGS} -DMGD_HAVE_DBUS "
	DBUS_CONF_DIR=`pkg-config --variable sysconfdir dbus-1`
fi
AM_CONDITIONAL(MIDGARD_DBUS_SUPPORT, test x$_MIDGARD_DBUS_SUPPORT = xyes)

LIBS="$INTLLIBS $LIBS"

PATH="${PATH}:${prefix}/bin"

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

AC_MSG_CHECKING([if building for some Win32 platform])
case "$host" in
  *-*-mingw*|*-*-cygwin*)
    platform_win32=yes
    ;;
  *)
    platform_win32=no
    ;;
esac
AC_MSG_RESULT($platform_win32)
AM_CONDITIONAL(PLATFORM_WIN32, test x$platform_win32 = xyes)

if test "$prefix" = "/usr"; then
	sysconfdir="/etc/midgardcr"
elif test "$prefix" = "NONE"; then
	sysconfdir="/usr/local/etc/midgardcr"
else 
	sysconfdir="$prefix/etc/midgardcr"
fi

LIBS="$LIBS $MIDGARD_LIBS"

AC_SUBST(LIBS)
AC_SUBST(VERSION)
AC_SUBST(VERSION_STRING)
AC_SUBST(LFLAGS)
AC_SUBST(MIDGARD_DBUS_SUPPORT)
AC_SUBST(LIBGDA_VERSION, $gdamodule)
AC_SUBST(MIDGARD_LIBS)
AC_SUBST(MIDGARD_CFLAGS)
AC_SUBST(DBUS_CONF_DIR)

AC_CONFIG_FILES([Makefile 
		 src/Makefile 
		 src/core/Makefile 
		 src/core/midgard_local.h
		 src/vala/Makefile 
		 midgardcr.pc])
AC_OUTPUT
